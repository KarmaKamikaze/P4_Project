Package STEP;

Helpers
  tab = 9;
  cr = 13;
  lf = 10;
  dblquote = 34;
  lowercase = ['a' .. 'z'];
  uppercase = ['A' .. 'Z'];
  letter = lowercase | uppercase | '_';
  digit = ['0' .. '9'];
  letter_or_digit = letter | digit;
  all_chars = [0 .. 65535];
  string_content = [all_chars - dblquote];

Tokens
  lparen = '(';
  rparen = ')';
  lbrack = '[';
  rbrack = ']';
  assign = '=';
  plus = '+';
  minus = '-';
  divide = '/';
  mult = '*';
  pow = '^';
  grthan = '>';
  grthaneq = '>=';
  lthan = '<';
  lthaneq = '<=';
  eq = '==';
  neq = '!=';
  neg = '!';
  whitespace = (' ' | tab)+;
  comma = ',';
  nl = cr? lf;
  numliteral = '-'? digit+ | '-'? digit+ '.' digit*;
  strliteral = dblquote string_content* dblquote;
  boolliteral = '!'? 'true'| '!'? 'false';

// Keywords
  setup = 'setup';
  endsetup = 'end setup';
  loop = 'loop';
  endloop = 'end loop';
  functions = 'functions';
  endfunctions = 'end functions';
  function = 'function';
  endfunction = 'end function';
  variables = 'variables';
  endvariables = 'end variables';
  blank = 'blank';
  number = 'number';
  string = 'string';
  boolean = 'boolean';
  if = 'if';
  endif = 'end if';
  else = 'else';
  continue = 'continue';
  break = 'break';
  repeatwhile = 'repeat while';
  endwhile = 'end while';
  repeatfor = 'repeat for';
  endfor = 'end for';
  to = 'to';
  increaseby = 'increase by';
  decreaseby = 'decrease by';
  switch = 'switch';
  endswitch = 'end switch';
  when = 'when';
  do = 'do';
  fallthrough = 'fallthrough';
  otherwisedo = 'otherwise do';
  return = 'return';
  and = 'and';
  or = 'or';
  constant = 'constant';
  id = letter letter_or_digit*; 

Ignored Tokens
  whitespace;

Productions
  // Program
  program = nl* P.variables? setuploop P.functions?;
  setuploop = {one} P.setup nl* | {two} P.loop nl* | {three} P.setup [fst]:nl* P.loop [snd]:nl*;
  setup = T.setup stmt* endsetup;
  loop = T.loop stmt* endloop;
  
  // Global variable declarations
  variables = T.variables var_or_nl* endvariables nl*;
  var_or_nl = {one} vardcl | {two} nl;
  
  // Function declarations
  functions = T.functions funcdcl_or_nl* endfunctions nl*;
  funcdcl = {one} type brackets* T.function id params stmt* retstmt [fst]:nl endfunction [snd]:nl | {two} blank T.function id params stmt* endfunction nl;
  funcdcl_or_nl = {one} funcdcl | {two} nl;
  
  brackets = lbrack rbrack;
  params = lparen params_content? rparen;
  params_content = type brackets* id params_multi*;
  params_multi = comma type brackets* id;
  type = {one} number | {two} string | {three} boolean;
  
  // Statements
  stmt = stmts nl;
  stmts = {one} ifstmt | {two} whilestmt | {three} forstmt | {four} vardcl | {five} assstmt | {six} funccall | {seven} retstmt | {eight} switchstmt |  ;
  ifstmt = {nonelse} if lparen cond rparen stmt* endif| {withelse} if lparen cond rparen [fst]:stmt* else [snd]:stmt* endif;
  whilestmt = repeatwhile lparen cond rparen option_stmt_continue_break* endwhile;
  option_stmt_continue_break = {one} stmt | {two} continue nl | {three} break nl;
  forstmt = repeatfor lparen forstmt_iterator_options to [fst]:expr comma option_increase_decrease [snd]:expr rparen option_stmt_continue_break* endfor;
  forstmt_iterator_options = {one} numdcl | {two} assstmt | {three} id arrindex?;
  option_increase_decrease = {one} increaseby | {two} decreaseby;
  switchstmt = switch lparen id arrindex? rparen whencase* otherwisecase? endswitch;
  whencase = when numliteral do stmt* fallthrough?;
  otherwisecase = otherwisedo stmt*;
  assstmt = id arrindex? assign expr;
  funccall = id lparen params_options? rparen;
  params_options = expr multi_expr*;
  multi_expr = comma expr;
  retstmt = {one} return cond | {two} return;
  arrindex = lbrack expr rbrack;
  
  // Arithmetic expressions
  expr = {one} expr plus term | {two} expr minus term | {three} term;
  term = {one} term mult factor | {two} term divide factor | {three} factor;
  factor = {one} factor pow value | {two} value;
  value = {one} P.constant | {two} id arrindex? | {three} lparen expr rparen | {four} funccall;
  constant = {one} numliteral | {two} strliteral | {three} boolliteral;
  
  // Boolean expressions
  cond = {one} neg? comp | {two} neg? [fst]:comp bop [snd]:cond;
  comp = {one} expr | {two} [fst]:expr relop [snd]:expr;
  relop = {one} grthan | {two} grthaneq | {three} lthan | {four} lthaneq | {five} eq | {six} neq;
  bop = {one} and | {two} or;
  
  // Variable declarations
  vardcl = T.constant? var_options;
  var_options = {one} numdcl | {two} stringdcl | {three} booldcl | {four} arrdcl;
  numdcl = number id assign expr;
  stringdcl = string id assign expr;
  booldcl = boolean id assign cond;
  arrdcl = type [fst]:arrsizedcl [multi]:arrsizedcl* id assign arr_id_or_expr;
  arr_id_or_expr = {one} id | {two} lbrack params_options? rbrack;
  arrsizedcl = lbrack numliteral rbrack;